buildscript {
    ext {
        // Kotlin
        kotlinVersion = '1.5.31'

        // Vertx
        vertxVersion = '4.3.1'
        nettyVersion = '4.1.77.Final'  // must match the version used by vertx (https://github.com/vert-x3/vertx-dependencies/blob/master/pom.xml)

        // Commons
        jacksonVersion = '2.13.3'

        // Hibernate
        mutinyVersion = '2.22.0'

        // AWS
        awsVersion = '1.12.252'

        // gRPC
        grpcVersion = '1.47.0'
        grpcKotlinVersion = '1.3.0'
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "org.jetbrains.kotlin:kotlin-noarg:$kotlinVersion"
    }
}

plugins {
    id 'java'
    id 'application'
    id 'org.jetbrains.kotlin.jvm' version "$kotlinVersion"
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'pl.allegro.tech.build.axion-release' version '1.13.6'
}

apply plugin: 'kotlin'
apply plugin: "kotlin-jpa"

scmVersion {
    useHighestVersion = true
    versionCreator 'versionWithBranch'
}

group 'com.github.mkorman9.vertx'
version scmVersion.version

def javaVersion = '11'
sourceCompatibility = javaVersion
targetCompatibility = javaVersion

repositories {
    mavenCentral()
    maven {
        url "https://mymavenrepo.com/repo/GgSDaevb7Ejr77jojEys/"  // contains kotlin-vertx-protocol
    }
}

dependencies {
    implementation "com.github.mkorman9.vertx:kotlin-vertx-protocol:0.1.0"

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
    testImplementation "org.jetbrains.kotlin:kotlin-test:$kotlinVersion"

    // Vertx
    implementation "io.vertx:vertx-core:$vertxVersion"
    implementation "io.vertx:vertx-web:$vertxVersion"
    implementation "io.vertx:vertx-lang-kotlin:$vertxVersion"
    implementation "io.vertx:vertx-lang-kotlin-coroutines:$vertxVersion"
    implementation "io.vertx:vertx-config:$vertxVersion"
    implementation "io.vertx:vertx-config-hocon:$vertxVersion"
    implementation "io.vertx:vertx-micrometer-metrics:$vertxVersion"
    implementation "io.netty:netty-transport-native-epoll:$nettyVersion:linux-x86_64"
    implementation "io.netty:netty-transport-native-kqueue:$nettyVersion:osx-x86_64"
    implementation 'io.micrometer:micrometer-registry-prometheus:1.9.0'
    testImplementation "io.vertx:vertx-junit5:$vertxVersion"

    // Commons
    implementation 'org.slf4j:slf4j-api:1.7.36'
    implementation 'ch.qos.logback:logback-classic:1.2.11'
    implementation 'de.siegmar:logback-gelf:4.0.2'
    implementation 'org.codehaus.janino:janino:3.1.7'
    implementation "com.fasterxml.jackson.core:jackson-databind:$jacksonVersion"
    implementation "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:$jacksonVersion"
    implementation "com.fasterxml.jackson.module:jackson-module-kotlin:$jacksonVersion"
    implementation "org.hibernate.validator:hibernate-validator:6.0.21.Final"
    implementation 'at.favre.lib:bcrypt:0.9.0'
    implementation 'org.quartz-scheduler:quartz:2.3.2'
    testImplementation 'io.mockk:mockk:1.12.4'
    testImplementation 'org.assertj:assertj-core:3.23.1'

    // Postgres
    implementation "io.vertx:vertx-pg-client:$vertxVersion"
    implementation "io.vertx:vertx-jdbc-client:$vertxVersion"
    implementation 'org.liquibase:liquibase-core:4.12.0'
    implementation 'org.postgresql:postgresql:42.4.0'
    implementation 'com.mattbertolini:liquibase-slf4j:4.1.0'

    // Redis
    implementation "io.vertx:vertx-redis-client:$vertxVersion"

    // Hibernate
    implementation 'org.hibernate.reactive:hibernate-reactive-core:1.1.6.Final'
    implementation "io.smallrye.reactive:smallrye-mutiny-vertx-core:$mutinyVersion"
    implementation "io.smallrye.reactive:smallrye-mutiny-vertx-pg-client:$mutinyVersion"

    // AWS
    implementation "com.amazonaws:aws-java-sdk-sqs:$awsVersion"
    implementation "com.amazonaws:aws-java-sdk-sns:$awsVersion"
    implementation "com.amazonaws:aws-java-sdk-s3:$awsVersion"
    implementation "com.amazonaws:aws-java-sdk-dynamodb:$awsVersion"

    // gRPC
    implementation "io.grpc:grpc-netty:$grpcVersion"
    implementation "io.grpc:grpc-protobuf:$grpcVersion"
    implementation "io.grpc:grpc-kotlin-stub:$grpcKotlinVersion"
    implementation "com.google.protobuf:protobuf-kotlin:3.21.1"
    testImplementation "io.grpc:grpc-testing:$grpcVersion"
}

compileKotlin {
    kotlinOptions {
        jvmTarget = javaVersion
    }
    sourceCompatibility = javaVersion
}

compileTestKotlin {
    kotlinOptions {
        jvmTarget = javaVersion
    }
    sourceCompatibility = javaVersion
}

test {
    useJUnitPlatform()
}

shadowJar {
    archiveFileName = 'app.jar'
    mainClassName = 'com.github.mkorman9.vertx.MainKt'

    manifest {
        attributes 'Version': project.version
    }

    mergeServiceFiles {
        include 'META-INF/services/io.vertx.core.spi.VerticleFactory'
    }
}
